#!python
import os
import subprocess
import sys

opts = Variables([], ARGUMENTS)
opts.Add(
    'ANDROID_NDK_ROOT',
    'Path to your Android NDK installation. By default, uses ANDROID_NDK_ROOT from your defined environment variables.',
    os.environ.get("ANDROID_NDK_ROOT", None)
)
opts.Add(
    'android_api_level',
    'Target Android API level',
    '18' if ARGUMENTS.get("android_arch", 'armv7') in ['armv7', 'x86'] else '21'
)
opts.Add(EnumVariable(
    'android_arch',
    'Target Android architecture',
    'arm64v8',
    ['armv7','arm64v8','x86','x86_64']
))
opts.Add(PathVariable(
    'osgeo_path',
    'Path to GDAL headers and libraries',
    None,
    PathVariable.PathIsDir
))

# Gets the standard flags CC, CCX, etc.
env = DefaultEnvironment()

# Define our options
opts.Add(EnumVariable('target', "Compilation target", 'debug', ['d', 'debug', 'r', 'release']))
opts.Add(EnumVariable('platform', "Compilation platform", '', ['', 'windows', 'x11', 'linux', 'osx', 'android']))
opts.Add(BoolVariable('use_llvm', "Use the LLVM / Clang compiler", 'no'))
opts.Add(PathVariable('osgeo_path', "(Windows and Mac) path to OSGeo installation", "", PathVariable.PathAccept))


# Try to detect the host platform automatically.
# This is used if no `platform` argument is passed
if sys.platform.startswith('linux'):
    host_platform = 'linux'
elif sys.platform == 'darwin':
    host_platform = 'osx'
elif sys.platform == 'win32' or sys.platform == 'msys':
    host_platform = 'windows'
elif sys.platform == 'android':
    host_platform = 'android'
else:
    host_platform = "Unknown platform: " + sys.platform


# only support 64 at this time..
bits = 64

# Updates the environment with the option variables.
opts.Update(env)
if env['platform'] == '':
    print("No valid target platform selected.")
    quit()

env['target_path'] = 'build/'
env['target_name'] = 'libRasterTileExtractor'

env.Append(CXXFLAGS=['-std=c++17', '-fPIC'])

# Check our platform specifics
if env['platform'] == "android":
    gdal_include_path = os.path.join(env['osgeo_path'], "include")
    gdal_lib_path = os.path.join(env['osgeo_path'], "lib")    
    gdal_lib_name = 'gdal'

    if host_platform == 'windows':
        env = env.Clone(tools=['mingw'])
        env["SPAWN"] = mySpawn

    # Verify NDK root
    if not 'ANDROID_NDK_ROOT' in env:
        raise ValueError("To build for Android, ANDROID_NDK_ROOT must be defined. Please set ANDROID_NDK_ROOT to the root folder of your Android NDK installation.")

    # Validate API level
    api_level = int(env['android_api_level'])
    if env['android_arch'] in ['x86_64', 'arm64v8'] and api_level < 21:
        print("WARN: 64-bit Android architectures require an API level of at least 21; setting android_api_level=21")
        env['android_api_level'] = '21'
        api_level = 21

    # Setup toolchain
    toolchain = env['ANDROID_NDK_ROOT'] + "/toolchains/llvm/prebuilt/"
    if host_platform == "windows":
        toolchain += "windows"
        import platform as pltfm
        if pltfm.machine().endswith("64"):
            toolchain += "-x86_64"
    elif host_platform == "linux":
        toolchain += "linux-x86_64"
    elif host_platform == "osx":
        toolchain += "darwin-x86_64"
    env.PrependENVPath('PATH', toolchain + "/bin") # This does nothing half of the time, but we'll put it here anyways

    # Get architecture info
    arch_info_table = {
        "armv7" : {
            "march":"armv7-a", "target":"armv7a-linux-androideabi", "tool_path":"arm-linux-androideabi", "compiler_path":"armv7a-linux-androideabi",
            "ccflags" : ['-mfpu=neon']
            },
        "arm64v8" : {
            "march":"armv8-a", "target":"aarch64-linux-android", "tool_path":"aarch64-linux-android", "compiler_path":"aarch64-linux-android",
            "ccflags" : []
            },
        "x86" : {
            "march":"i686", "target":"i686-linux-android", "tool_path":"i686-linux-android", "compiler_path":"i686-linux-android",
            "ccflags" : ['-mstackrealign']
            },
        "x86_64" : {"march":"x86-64", "target":"x86_64-linux-android", "tool_path":"x86_64-linux-android", "compiler_path":"x86_64-linux-android",
            "ccflags" : []
        }
    }
    arch_info = arch_info_table[env['android_arch']]

    # Setup tools
    env['CC'] = toolchain + "/bin/clang"
    env['CXX'] = toolchain + "/bin/clang++"
    env['AR'] = toolchain + "/bin/" + arch_info['tool_path'] + "-ar"
    env.Append(CPPPATH=[gdal_include_path])

    env.Append(CCFLAGS=['--target=' + arch_info['target'] + env['android_api_level'], '-march=' + arch_info['march'], '-fPIC'])#, '-fPIE', '-fno-addrsig', '-Oz'])
    env.Append(CCFLAGS=arch_info['ccflags'])
elif env['platform'] in ('x11', 'linux'):
    gdal_include_path = ""
    gdal_lib_path = ""
    gdal_lib_name = "libgdal"

    # Arch needs different includes!
    import distro
    if distro.like() == "arch" or distro.id() == "arch":
        env.Append(CPPDEFINES=["_ARCH"])

elif env['platform'] == "windows":
    env.Replace(CXX=['/usr/bin/x86_64-w64-mingw32-g++'])
    env.Append(LINKFLAGS=['-static-libgcc', '-static-libstdc++'])

    # Include GDAL
    gdal_include_path = os.path.join(env['osgeo_path'], "include")
    gdal_lib_path = os.path.join(env['osgeo_path'], "lib")
    gdal_lib_name = "gdal"

    if not os.path.exists(gdal_include_path) or not os.path.exists(gdal_lib_path):
        print("OSGeo paths are invalid!")
        quit()

elif env['platform'] == 'osx':
    # Include GDAL
    gdal_include_path = os.path.join(env['osgeo_path'], "include")
    gdal_lib_path = os.path.join(env['osgeo_path'], "lib")
    gdal_lib_name = "libgdal.dylib"

    if not os.path.exists(gdal_include_path) or not os.path.exists(gdal_lib_path):
        print("OSGeo paths are invalid!")
        quit()

env.Append(CPPPATH=[gdal_include_path])

# tweak this if you want to use different folders, or more folders, to store your source code in.
env.Append(CPPPATH=['./'])
env.Append(CPPPATH=['../global/'])
sources = Glob('./*.cpp')

library = env.StaticLibrary(target=env['target_path'] + env['target_name'], source=sources)
